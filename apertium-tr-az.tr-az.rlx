###############################################################################
#
#	A Constraint Grammar Disambiguator for Turkish
#
#
#

DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) ; # Beginning of sentence
LIST EOS = (<<<) ; # End of sentence

LIST N = (n) (nexist) ;
LIST V = (v) (exist) (nexist) (not) ;
LIST V/SP = (exist) (nexist) ;
LIST A = (adj) ;
LIST Adv = (adv) ;
LIST Det = (det) ;
LIST Post = (postp) ;
LIST Pron = (prn) ;
LIST CC = (cnjcoo) ;
LIST CS = (cnjsub) ;
LIST Num = (num) ;
LIST NOT = (not) ;
LIST NP = (np) ;


LIST Acc = (acc) ;
LIST Dat = (dat) ;
LIST Gen = (gen) ;
LIST Ins = (ins) ;
LIST Abl = (abl) ;
LIST Loc = (loc) ;

LIST MARKED-Cases = (acc dat gen ins abl loc) ;

SET Abs = N - MARKED-Cases ;


LIST Sg1 = (1s) ;
LIST Sg2 = (2s) ;
LIST Sg3 = (3s) ;
LIST Pl1 = (1p) ;
LIST Pl2 = (2p) ;
LIST Pl3 = (3p) ;

LIST Be = (1s 2s 3s 1p 2p 3p) ;

LIST PxSg1 = (p1s) ;
LIST PxSg2 = (p2s) ;
LIST PxSg3 = (p3s) ;
LIST PxPl1 = (p1p) ;
LIST PxPl2 = (p2p) ;
LIST PxPl3 = (p3p) ;

LIST Poss = (p1s) (p2s) (p3s) (p1p) (p2p) (p3p) ;

 
LIST Imper = (t_imp) ; 

LIST CV-MARKER = (cv) (cv_dik) (cv_acak) (cv_ma) (cv_mak) (cv_dan) (cv_zdan) (cv_ince) (cv_erek) (cv_erek) (cv_eli) (cv_ene) (cv_ir) (cv_ar) (cv_ecek) (cv_mis) (cv_iyor) (cv_cesine) (cv_ip);


LIST VN/acak = (vn_acak) ;
LIST VN/dik = (vn_dik) ;
LIST VN/ma = (vn_ma) ;
LIST VN/mak = (vn_mak) ;
LIST VN/yis = (vn_yis) ;
LIST CP/ken = (v cpl_ken) ;



LIST Der/0/Adv→N = (adv Dan_0 n) ;
LIST Der/0/A→N = (adj Djn_0 n) ;
LIST Der/lAn = (n D_lAn v) ;
LIST Der/CI = (v D_CI n) ;
LIST Der/yIS = (v D_yIS n) ;
LIST Der/lIk = (n D_lIK n) ;
LIST Der/lI = (n D_lI n) ;

LIST Der/mA = (v D_mA n) ;
LIST Der/lA = (n D_lA v) (adj D_lA v) ;
LIST NP/Sal = (np D_sAl) ;
LIST N/Sal = (n D_sAl) ;
LIST A/Sal = (adj D_sAl) ;
LIST Adv/Dan = (adv Dan_0 n) ;
LIST N/dcak = (n dcak n) ;
LIST N/ci = (n ci n) ;
LIST N/lI = (n dli) ;
LIST Num/Dmn_O = (num Dmn_0 n) ;



LIST POST-ABS = ("bakımdan"i postp) ("hakkında"i postp) ("tarafından"i postp) ("yüzünden"i postp) ("için"i postp) ;
LIST POST-DAT = ("doğru"i postp) ("göre"i postp) ("kadar"i postp) ("karşı"i postp) ;
LIST POST-ABL = ("önce"i postp) ("sonra"i postp) ("beri"i postp) ("itibaren"i postp) ("dolayı"i postp) ("başka"i postp) ("rağmen"i postp) ("yana"i postp) ;
LIST POST-TEM = ("sırasında" N PxSg3 loc *3s) ;

LIST FINITE = t_narr t_aor t_cont t_fut t_past nexist ; 

SET FINITE-NOSUB = FINITE - CP/ken ;

SET PERS = Sg1 | Sg2 | Sg3 | Pl1 | Pl2 | Pl3 ;
SET POSS = PxSg1 | PxSg2 | PxSg3 | PxPl1 | PxPl2 | PxPl3 ;
SET NOMINAL = N | A ;
SET NOM-OR-PRO = NOMINAL | Pron ;
SET WORD = N | A | V | Det | Post | Pron | CC | CS ;

SET VN = VN/acak | VN/dik | VN/ma | VN/mak | VN/yis ; 

SET OBL-CASE = Acc | Dat | Gen | Ins | Abl ;

SET CLB = CP/ken ;


SECTION

###############################################################################
## Bad rules
###############################################################################





###############################################################################
## Remove redundant derivations 
###############################################################################
# Derivations are cool, and should be included for sure in a serious morphological analysis
# We don't want them in the transfer though, cause they are problematic to handle
#
#



######### RULES N + N CHECK CASES. IT IS POSSIBLE TO HAVE 2 NOUNS IN A SEQUENCE, CHECK CASES like GIANLUCA ARABAYI KULLANIYOR

REMOVE NP/Sal ;

REMOVE N/Sal ;

REMOVE A/Sal ;

REMOVE N/dcak ;

REMOVE Adv/Dan;

REMOVE Num/Dmn_O ;
     ## We really don't want those derivations


REMOVE Der/0/A→N IF (0 Der/0/A→N) (0 N) ;
    ## içindekiler
    
REMOVE Der/0/A→N IF (0 Der/0/A→N) (0 A) ;

REMOVE Der/lI IF (0 Der/lI) (0 A OR N) ; 

REMOVE Der/0/Adv→N IF (0 Der/0/Adv→N) (0 Adv);
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE Der/CI IF (0 N) (0 Der/CI) ;
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE Der/lAn IF (0 V) ;
    ## Aslında MKK'nin iki yıllık bir proje olması planlanmış.

REMOVE Der/yIS IF (0 N) (0 Der/yIS) ;
    ## Bilişim ve haberleşe teknolijlerinin asıl altyapı göstergelerinde biri olan telefon iletişimi ülke halkının en çok istifade ettiği iletişim aracı olmaya devam etmektedir. 

REMOVE Der/lIk IF (0 N) (0 Der/lIk) ;
    ## 

REMOVE Der/mA IF (NOT 0C V) (0 Der/mA) ;

REMOVE Der/lA IF (0 Der/lA) ;

    ## All of this DER/* rules are to cut out derivatives, which are cool but create problems during the translation
    
    





###############################################################################
## NP internal
###############################################################################

REMOVE Dat IF (1C Dat) ; 
    ## Rule 1
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE Acc IF (NOT 0 POSS) (-1 Gen);
    ## Rule 2

SELECT A IF (-1 A LINK -1 Det) (1 N LINK NOT 0 POSS) ;
    ## Rule 3

SELECT N + PxSg2 IF (-1 Sg2 + Gen) ;
    ## Rule 4

###############################################################################
## Disambiguate postpositions 
###############################################################################

REMOVE Acc IF (1C POST-ABS) ;
    ## Rule 5

SELECT Abl IF (1C POST-ABL) ;
    ## Rule 6

REMOVE POST-ABS IF (-1C OBL-CASE);
    ## Rule 7
    ## 
    ## 
    
REMOVE CC IF (1 EOS) ;

   ## Rule 30
   ## The reading cnjcoo is dropped if it is at the end of the sentence
   ## Hepiniz bundan için.

###############################################################################

## REMOVE NOM-OR-PRO + Sg3 IF (1C* V + PERS BARRIER EOS) ;
    ## Rule 8
    ## Ondan sonraysa yüzde 6,75'e yükseliyor.
    ## Eve girmekteyken, bir şey hatırladım.

REMOVE N IF (0 N) (0 FINITE) (1 EOS) ;
    ## Rule 9
    ## Içerler

REMOVE V IF (0 V) (0 NOT)  (NOT 1* WORD  BARRIER EOS);
    ## Rule 10

REMOVE CV-MARKER IF (NOT 1* WORD BARRIER EOS) ;
    ## Rule 11
    ## Ahmet dersine çalışıyor.
    ## Çok içmiş gibi konuşuyor.

REMOVE Imper IF (NOT 1* ("!")) ;
    ## Rule 12 

REMOVE CV-MARKER IF (0 N) (0 CV-MARKER)  ; 
    ## Rule 13
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE CV-MARKER IF (0 V) (0 CV-MARKER) (1 ("ki"i)) ;

   ## Rule 14
   ## Converbs cannot be before "ki", so that this rule takes CV out when followed by ki

REMOVE CV-MARKER IF (0 V) (1 FINITE) ;


REMOVE VN IF (0 N) (0 VN)  (1 POST-TEM) ;
    
    ## Rule 15
    ## [0]Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE N IF (0 N) (0 A) (1 Num) (2 N) ;
    
    ## Rule 16
    ## This is a really common structure in turkish Adj + Bir + N
    ## [0]yeni bir araba

SELECT Det IF (0 A) (0 Num) (0 Det) (-1 A) ;
    ## Rule 17
    ## yeni [0]bir now selects A+Det
    
SELECT Det if (0 A) (0 Num) (0 Det) (1 N) ;
    
    ## Rule 18
    ## [0]bir orman now selects Det+Num
    
    
SELECT A if (0 A) (0 N) (NOT 0 Ins) (1 N) (NOT 1 Ins) ;

     ## Rule 19
     ## This rule removes the reading of [0] as noun in case it has no INSTRUMENTAL CASE, which is used in turkish as
     ## conjunction sometimes
     ## [0]aynı anda ## Forms of the type Yeniyle Motosiklet are left <n> + <n>
     
SELECT N if (0 ("Günaydın"i)) ;

    ## Rule 20
    ## TO BE REMOVED, it is really disgusting!


SELECT Adv IF (0 ("en"i)) (1 A OR Adv) ; 
    
    ## Rule 21 
    ## This rule selects <adv> for "en" in case it is followed by an adjective
    ## Dünyanın [0]en güzel kızı evime geldi.
    

SELECT PxSg3 IF (0 Acc OR PxSg3)  (NOT -1* POSS BARRIER Gen) ; # (-1* Gen BARRIER BOS) ;
    
    ## Rule 22
    ## Dünyanın en güzel kızı evime geldi. 

REMOVE PxSg2 IF (0 ("dünya"i)) (0C Gen) (1 ("en"i)) (NOT -1 ("senin"i));
    
    ## Rule 23
    ## [0]Dünyanın en güzel kızı evime geldi.
    
REMOVE N IF (0 ("verdi"i)) ((-2 Dat) OR (-1 Dat)) ;

  ## Rule 24
  ## vermek has such a structure [dat ?unmarked? vermek]
  ## Bana ekmek [0]verdi
  ## Bana [0]verdi
  ## This rule removes the reading of verdi as <n> in case we have a dative in -2 or -1 position
    
SELECT V IF (NOT -1* FINITE-NOSUB BARRIER CLB) (0 FINITE-NOSUB) ;

  ## Rule 25
  ## This rule, where there is ambiguity for the [0]word, checks if any word preceding [0]
  ## is a finite verb. If not, it select, where possible, the [0]word as a verb.
  
SELECT V IF (0 V/SP) (-1 Poss) ;

    ## Rule 26
    ## Test RULE. Selects V (i.e. in this case exist or nexist realized as var/yok) if the precedent word has
    ## a possessive in it. It's the typical construction for turkish for "to have" poss + var.
    ## Dikkat: It should be done probably for all the finite verbs in place of V/SP
    ## Problems: Bilim var! == i have information or "there is science"

SELECT Poss IF (1 V/SP) ;

   ## Rule 27
   ## If the following word is V/SP (i.e. var or yok), Poss is selected over the verb "to be" (in the morph
   ## analyzer 1s2s3s 1p2p3p
   
SELECT Poss IF (0 Poss) (-1 N) ;

  ## Rule 28
  ## This rule should select Possessive for patterns like N + N+poss. This patters are called "unmarked compound noun"
  ## i.e. compound nouns where the first noun is not marked with GEN.
  

SELECT Abl if (0 N)  (1 POST-ABL) ;

 ## Rule 29
 ## IT handles cases like "[0]baştan beri", where baştan can be read both as n and adv.
 ## Beri, meaning since or from, in a temporal way, has a construction of the type n+abl + beri
 ## 

SELECT Dat if (0 N) (1 POST-DAT) ;

 ## Rule 30
 ## Same as rule 29
 
REMOVE Gen if (1 Gen) ;


 
 


    
    
SELECT Det IF (0 Det) ;

    
    ## Dirty!! To be removed after <prn><dem>
    
SELECT NP if (0 NP) ;

SELECT Pron IF (0 (<bu>)) ;

   
   ## Dirty!! To be removed 

REMOVE (n 3p);

   ## TO BE REMOVED
   








