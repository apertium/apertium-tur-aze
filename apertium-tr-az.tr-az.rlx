###############################################################################
#
#	A Constraint Grammar Disambiguator for Turkish
#
#
#

DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) ; # Beginning of sentence
LIST EOS = (<<<) ; # End of sentence

LIST N = (n) ;
LIST V = (v) ;
LIST A = (adj) ;
LIST Det = (det) ;
LIST Post = (postp) ;
LIST Pron = (prn) ;
LIST CC = (cnjcoo) ;
LIST CS = (cnjsub) ;
LIST NOT = (not) ;

LIST Acc = (acc) ;
LIST Abs = (abs) ;
LIST Dat = (dat) ;
LIST Gen = (gen) ;
LIST Ins = (ins) ;
LIST Abl = (abl) ;

LIST Sg1 = (1s) ;
LIST Sg2 = (2s) ;
LIST Sg3 = (3s) ;
LIST Pl1 = (1p) ;
LIST Pl2 = (2p) ;
LIST Pl3 = (3p) ;

LIST PxSg1 = (p1s) ;
LIST PxSg2 = (p2s) ;
LIST PxSg3 = (p3s) ;
LIST PxPl1 = (p1p) ;
LIST PxPl2 = (p2p) ;
LIST PxPl3 = (p3p) ;

LIST Imper = (t_imp) ; 

LIST CV-MARKER = (cv) ;
LIST VN = (vn) ;

LIST Der0/A→N = (adj Djn_0 n) ;
LIST Der/CI = (v D_CI n) ;

LIST POST-ABS = ("bakımdan"i postp) ("hakkında"i postp) ("tarafından"i postp) ("yüzünden"i postp) ;
LIST POST-DAT = ("doğru"i postp) ("göre"i postp) ("kadar"i postp) ("karşı"i postp) ;
LIST POST-ABL = ("önce"i postp) ("sonra"i postp) ("beri"i postp) ("itibaren"i postp) ("dolayı"i postp) ;

SET PERS = Sg1 | Sg2 | Sg3 | Pl1 | Pl2 | Pl3 ;
SET POSS = PxSg1 | PxSg2 | PxSg3 | PxPl1 | PxPl2 | PxPl3 ;
SET NOMINAL = N | A ;
SET NOM-OR-PRO = NOMINAL | Pron ;
SET WORD = N | A | V | Det | Post | Pron | CC | CS ;

SECTION

REMOVE Acc IF (1C POST-ABS) ;
    ##

REMOVE Acc IF (NOT 0 POSS) (-1 Gen);
    ##

SELECT Abl IF (1C POST-ABL) ;
    ##

SELECT A IF (-1 A LINK -1 Det) (1 N LINK NOT 0 POSS) ;
    ##

SELECT N + PxSg2 IF (-1 Sg2 + Gen) ;
    ##

REMOVE NOM-OR-PRO + Sg3 IF (1C* V + PERS BARRIER EOS) ;
    ## Ondan sonraysa yüzde 6,75'e yükseliyor.
    ## Eve girmekteyken, bir şey hatırladım.

REMOVE N IF (0 N) (0 V) (NOT -1* WORD BARRIER BOS) (NOT 1* WORD BARRIER EOS);
    ## Içerler

REMOVE V IF (0 V) (0 NOT)  (NOT 1* WORD  BARRIER EOS);

REMOVE CV-MARKER IF (NOT 1* WORD BARRIER EOS) ;
    ## Ahmet dersine çalışıyor.

REMOVE Imper IF (NOT 1* ("!")) ;

REMOVE Der0/A→N IF (0 Der0/A→N) (0 N);
    ## içindekiler

REMOVE Der/CI IF (0 N) (0 Der/CI) ;
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE CV-MARKER IF (0 N) (0 CV-MARKER) ; 
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.

REMOVE VN IF (0 N) (0 VN) ;
    ## Yemek sırasında İsa eline ekmek aldı, şükredip ekmeği böldü ve öğrencilerine verdi.
